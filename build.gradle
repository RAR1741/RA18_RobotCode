plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2018.03.06"
    id 'net.saliman.cobertura' version '2.5.3'
}

def ROBOT_CLASS = "org.redalert1741.powerup.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", jaci.openrio.gradle.frc.RoboRIO) {
            team = Integer.parseInt(TEAM)
        }
    }
    artifacts {
        artifact('frcJava', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << "roborio"
        }

        fileArtifact('logging.properties') {
            file = file('logging.properties')
            filename = 'logging.properties'
            targets << "roborio"
        }

        fileCollectionArtifact('autoFiles') {
            directory = 'auto'
            files = fileTree(dir: 'auto')
            targets << "roborio"
        }
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

tasks.discoverRoborio.doFirst { println "Connecting to: " + TEAM }

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and NavX.
dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile openrio.powerup.matchData()
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    testCompile 'junit:junit:4.12'
    testRuntime "org.slf4j:slf4j-api:1.7.10"
    compile group: 'net.saliman', name: 'gradle-cobertura-plugin', version: '2.5.3'
    // compile group: 'org.apache.logging.log4j.jul.LogManager'

}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}

cobertura {
    coverageFormats = [ 'xml', 'html' ]
    coverageExcludes = ['.*org.redalert1741.robotbase.wrapper.*', '.*org.redalert1741.powerup.Robot.*']
}
